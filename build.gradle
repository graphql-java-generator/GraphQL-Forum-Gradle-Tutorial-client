// The GraphQL plugin is compiled with Spring Boot 2.7.
// If you want to use it with Spring Boot 3.x, you have to update these dependencies, like this buildscript block: 
buildscript {
    repositories {
    	// The GraphQL plugin depends on libraries that are available with maven
        mavenLocal()
        mavenCentral()
    }
	dependencies {
		classpath "org.springframework.boot:spring-boot-starter-graphql:${springBootVersion}"
    }
}

plugins {
	// CAUTION !    The id of the plugin has changed since 2.0RC1.
	// It was 'com.graphql_java_generator.graphql-gradle-plugin'. It is now 'com.graphql-java-generator.graphql-gradle-plugin3'
	id "com.graphql-java-generator.graphql-gradle-plugin3" version "${graphQLPluginVersion}"
	id 'java'
	id 'org.springframework.boot' version "${springBootVersion}"
	id 'io.spring.dependency-management' version "${springDependencyManagementPluginVersion}"
}


repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot'
	// THE VERSION MUST BE THE SAME AS THE PLUGIN's ONE
	implementation "com.graphql-java-generator:graphql-java-client-runtime:${graphQLPluginVersion}"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of("${javaVersion}")
    }
}
compileJava {
    options.encoding = "UTF-8"
	options.compilerArgs << "-Werror"
}

// The line below adds the generated sources as a java source folder in the IDE
sourceSets.main.java.srcDirs += '/build/generated/sources/graphqlGradlePlugin'
sourceSets.main.java.srcDirs += '/build/generated/resources/graphqlGradlePlugin'


// Let's configure the GraphQL Gradle Plugin:
// All available parameters are described here: 
// https://graphql-maven-plugin-project.graphql-java-generator.com/graphql-maven-plugin/generateClientCode-mojo.html
generateClientCodeConf {
	packageName = 'org.forum.client'
	customScalars = [ [
			graphQLTypeName: "Date",
			javaType: "java.util.Date",
			graphQLScalarTypeStaticField: "com.graphql_java_generator.customscalars.GraphQLScalarTypeDate.Date"
	] ]
}

	